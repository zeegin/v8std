###### #std630

# Правила создания модулей форм

###### 1.

Размещайте в модуле формы только код, который нужен для работы этой формы и выполняется в ее контексте.

###### 1.1.

Не размещайте экспортные процедуры и функции в модулях форм.
Для программного интерфейса используйте модули объектов, модули менеджеров объектов или общие модули.
Для передачи параметров используйте `#!bsl ОткрытьФорму`, для получения результата — обработчики оповещений, для взаимодействия между открытыми формами — `#!bsl Оповестить` и обработчик `#!bsl ОбработкаОповещения`.

###### 1.2.

Не используйте экспортные процедуры и функции формы для параметризации формы при открытии.

!!! failure "Неправильно"

    ```bsl
    Форма = ПолучитьФорму("ОбщаяФорма.МояФорма");
    Форма.Открыть();
    Форма.УстановитьПараметрСПомощьюЭтойЭкспортнойФункции(РежимРаботы);
    ```

!!! success "Правильно"

    ```bsl
    ПараметрыФормы = Новый Структура("РежимРаботы", РежимРаботы);
    ОткрытьФорму("ОбщаяФорма.МояФорма", ПараметрыФормы);
    ```

###### 1.3.

Не вызывайте экспортные процедуры и функции модуля формы для обновления данных формы или для программной перерисовки формы по действиям пользователя в других формах.
Используйте метод глобального контекста `#!bsl Оповестить` и методы формы `#!bsl ОповеститьОЗаписиНового`, `#!bsl ОповеститьОбАктивизации`, `#!bsl ОповеститьОВыборе`.

###### 1.4.

Исключение — экспортные процедуры-обработчики оповещений (`#!bsl ОписаниеОповещения.ИмяПроцедуры`).

###### 2.

Если одна и та же логика нужна на клиенте и на сервере, не дублируйте код.
Размещайте общую процедуру в модуле формы с директивой `#!bsl &НаКлиентеНаСервереБезКонтекста` и передавайте контекст параметром.
В качестве контекста используйте форму (`#!bsl ФормаКлиентскогоПриложения`) или реквизит формы (`#!bsl ДанныеФормыСтруктура`).

!!! example "Пример"

    ```bsl
    &НаСервере
    Процедура ПриСозданииНаСервере()
        УстановитьДоступность(ЭтотОбъект);
    КонецПроцедуры

    &НаКлиенте
    Процедура РазрешитьРедактированиеСуммыПриИзменении(Элемент)
        УстановитьДоступность(ЭтотОбъект);
    КонецПроцедуры

    &НаКлиентеНаСервереБезКонтекста
    Процедура УстановитьДоступность(Форма)
        Форма.Элементы.ТоварыСумма.Доступность = Форма.Объект.РазрешитьРедактированиеСуммы;
    КонецПроцедуры
    ```

###### См. также

- [#std455: Структура модуля](455.md)
- [#std404: Открытие форм](404.md); 
- [#std558: Обновление списков при интерактивных действиях пользователя](558.md).
- [#std636: Контекстная и внеконтекстная передача управления на сервер](636.md)

###### Источник

https://its.1c.ru/db/v8std#content:630
