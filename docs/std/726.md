###### #std726

# Особенности использования в запросах оператора ПОДОБНО

###### 1.

Оператор `#!sdbl ПОДОБНО` в тексте запроса используйте только

- с константным строковым литералом, например:
`#!sdbl Реквизит ПОДОБНО "123%"`
- с параметром запроса, например:
`#!sdbl Реквизит ПОДОБНО &Шаблон`

Не создавайте строку шаблона вычислениями или конкатенацией.


!!! failure "Неправильно"

    ```sdbl
    Реквизит ПОДОБНО "123" + "%"
    Реквизит ПОДОБНО &Шаблон + "%"
    Реквизит ПОДОБНО Таблица.Шаблон
    ```

!!! success "Правильно"

    ```sdbl
    Реквизит ПОДОБНО "123%"
    Реквизит ПОДОБНО &Шаблон
    ```

!!! quote "В стандарте не указано"

    PostgreSQL и Oracle Database:

    Квадратные скобки `#!sdbl […]` в шаблоне работают как спецсимволы в строковом литерале, и не работают в выражении.

    ```sdbl title="Скобки имеют специальный смысл"
    ВЫБРАТЬ * Справочник.Товары ГДЕ Артикул ПОДОБНО "123[АБ]%"
    ```

    ```sdbl title="Скобки в переменной &Шаблон это просто текст"
    ВЫБРАТЬ * Справочник.Товары ГДЕ Артикул ПОДОБНО &Шаблон + "%"
    ```

    ```sdbl title="Скобки в переменной &Шаблон имеют специальный смысл"
    ВЫБРАТЬ * Справочник.Товары ГДЕ Артикул ПОДОБНО &Шаблон
    ```

    - [Приложение 8. Особенности работы с различными СУБД](https://its.1c.ru/db/v851doc#bookmark:dev:TI000001291)

###### 2.

Используйте такие шаблоны оператора `#!sdbl ПОДОБНО`, которые работают одинаково на всех поддерживаемых СУБД.

Допустимо:

- `#!sdbl %` (процент) – последовательность, содержащая любое количество произвольных символов;
- `#!sdbl _` (подчеркивание) – один произвольный символ.

Не работает в IBM DB2:

- `#!sdbl […]` (в квадратных скобках один или несколько символов) – любой одиночный символ из перечисленных внутри квадратных скобок. В перечислении могут встречаться диапазоны, например `#!sdbl [a-z]`, означающие произвольный символ, входящий в диапазон, включая концы диапазона;
- `#!sdbl [^…]` (в квадратных скобках значок отрицания, за которым следует один или несколько символов) – любой одиночный символ, кроме тех, которые перечислены следом за значком отрицания.

Ограничение в MS SQL 2005:

- Длина строки шаблона не должна превышать `1024` символа

###### 3.

В шаблонах подобия используются спецсимволы `#!sdbl _%[]^`.
Шаблоны подобия могут формироваться динамически, по введенным данным пользователя.
В введенных пользователю данных могут использоваться спецсимволы, которые необходимо трактовать как данные пользователя. Для того, чтобы спецсимвол трактовался как обычный символ его необходимо экранировать.

!!! example "Например"

    Пользователь вводит текст: `Кефир 15% жирности`.
    Программа должны этот `%` трактовать как часть строки, а не как спецсимвол последовательности произвольных символов.

!!! danger "Ошибка"

    При работе на PostgreSQL можно встретить ошибку
    `#!log ERROR:  invalid regular expression: invalid character range` при выполнении запроса с некорректно собранным фрагментом содержащим `[]`.

Для экранирования:

- выберите спецсимвол экранирования, например `~`;
- установите спецсимвол перед символами операнда, например `~%`. Чтобы экранировать выбранный спецсимвол, его необходимо задвоить, например `~~`.
- дополните шаблон ключевым словом `#!sdbl СПЕЦСИМВОЛ`. 

!!! example "Например"

    Для поиск по строке `100%` 
    ```sdbl
    "100~%" СПЕЦСИМВОЛ "~"
    ```

    Для поиск по строке `Кефир 15% жирности` 
    ```sdbl
    "Кефир 15~% жирности" СПЕЦСИМВОЛ "~"
    ```

    Для поиск по строке `Шуруп_10[21] медь~4%` 
    ```sdbl
    "Шуруп~_10~[21~] медь~~4~%" СПЕЦСИМВОЛ "~"
    ```

Установка параметров запроса с помощью `#!bsl Запрос.УстановитьПараметр()` не экранирует спецсимволы шаблона подобия.

Для экранирования используйте функцию `#!bsl СформироватьСтрокуДляПоискаВЗапросе()`

!!! example "Например"

    ```sdbl
    ВЫБРАТЬ
        Номенклатура.Ссылка КАК Ссылка 
    ИЗ
        Справочник.Номенклатура КАК Номенклатура
    ГДЕ
        Номенклатура.Наименование ПОДОБНО &Шаблон СПЕЦСИМВОЛ "~"
    ```

    ```bsl
    Запрос.УстановитьПараметр("Шаблон", "%" + ОбщегоНазначения.СформироватьСтрокуДляПоискаВЗапросе(ИскомыйТекст) + "%");
    ```

!!! tip "Паттерн"

    ```bsl
    Функция СформироватьСтрокуДляПоискаВЗапросе(Знач СтрокаПоиска) Экспорт

        Результат = СтрокаПоиска;
        Результат = СтрЗаменить(Результат, "~", "~~");
        Результат = СтрЗаменить(Результат, "%", "~%");
        Результат = СтрЗаменить(Результат, "_", "~_");
        Результат = СтрЗаменить(Результат, "[", "~[");
        Результат = СтрЗаменить(Результат, "]", "~]");
        Результат = СтрЗаменить(Результат, "^", "~^"); 

        Возврат Результат;

    КонецФункции
    ```

    В 1С:БСП реализован в `#!bsl ОбщегоНазначения.СформироватьСтрокуДляПоискаВЗапросе()`.

###### 4.

Сравнение выполняется без учета регистра символов.

###### Источник

https://its.1c.ru/db/v8std#content:726