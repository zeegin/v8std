###### #std437

# Оформление текстов запросов

###### 1.

Все ключевые слова языка запросов пишутся заглавными буквами.

###### 2.

Указывайте необязательные части запроса. Явно задавайте псевдонимы полям. Так запрос проще читать, а код, который использует результат, меньше зависит от названий реквизитов. 

!!! failure "Неправильно"
    ```sdbl
    Касса.Валюта
    ```

    При изменении имени реквизита придется менять и код, который обращается к свойству `#!sdbl Валюта` в выборке. 

!!! success "Правильно"
    ```sdbl
    Касса.Валюта КАК Валюта
    ```

    Изменение имени реквизита приведет только к изменению текста запроса.

[#bslls:AssignAliasFieldsInQuery](../diagnostics/bslls/AssignAliasFieldsInQuery.md)

###### 2а.

Для полей – реквизитов других полей, например `#!sdbl Касса.Валюта.Наименование` автоматически присваиваемые псевдонимы собираются как `#!sdbl ВалютаНаименование`, а не `#!sdbl Наименование`.

[#bslls:AssignAliasFieldsInQuery](../diagnostics/bslls/AssignAliasFieldsInQuery.md)

###### 2б.

Пишите ключевое слово `#!sdbl КАК` перед псевдонимом поля источника.

[#bslls:AssignAliasFieldsInQuery](../diagnostics/bslls/AssignAliasFieldsInQuery.md)

###### 3.

Текст запроса должен быть структурирован. Не пишите запрос в одну строку, даже короткий. Наглядная структура упрощает чтение и поддержку другими разработчиками.

!!! failure "Неправильно"

    ```sdbl
    ВЫБРАТЬ Т.Наименование КАК Наименование ИЗ Справочник.Номенклатура КАК Т
    ```

!!! success "Правильно"

    ```sdbl
    ВЫБРАТЬ
        Т.Наименование КАК Наименование
    ИЗ
        Справочник.Номенклатура КАК Т
    ```

###### 4.

В сложных запросах используйте комментарии. Это особенно полезно при вложенных запросах, объединениях и соединениях. Комментарии помогают понять, зачем нужна таблица или блок запроса.

Помните: при использовании конструктора запросов все комментарии удаляются автоматически без предупреждения.

###### 5.

При создании объекта `#!bsl Запрос` указывайте комментарий о цели запроса: какие данные нужны или для чего они используются.

###### 6.1.

При программной "сборке" текста запроса комментируйте все этапы.

###### 6.2.

Старайтесь, чтобы каждая часть формируемого запроса открывалась в конструкторе запросов:

- можно быстро проверить синтаксис
- проще разрабатывать и сопровождать конфигурацию, в том числе сторонним разработчикам

###### 7.

Типичные случаи программной модификации текста запроса.

###### 7.1.

Изменение имени поля выборки.

!!! failure "Неправильно"

    ```bsl hl_lines="4"
    ТекстЗапроса =
        "ВЫБРАТЬ
        |   Номенклатура.Наименование  КАК Наименование ,
        |   Номенклатура. " + ИмяПоляКод + " КАК КодАртикул
        |ИЗ
        |   Справочник.Номенклатура КАК Номенклатура";
    ```

!!! success "Правильно"

    ```bsl hl_lines="4 8"
    ТекстЗапроса =
        "ВЫБРАТЬ
        |   Номенклатура.Наименование  КАК Наименование,
        |   &ИмяПоляКод  КАК КодАртикул
        |ИЗ
        |   Справочник.Номенклатура КАК Номенклатура";

    ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяПоляКод", "Номенклатура." + ИмяПоляКод);
    ```

Изменение имени таблицы.

!!! success "Правильно"

    ```bsl hl_lines="6 8"
    ТекстЗапроса =
        "ВЫБРАТЬ
        |   ТаблицаСправочника.Наименование КАК Наименование,
        |   ТаблицаСправочника.Код КАК Код
        |ИЗ
        |   &ТаблицаСправочника КАК ТаблицаСправочника";

    ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТаблицаСправочника", "Справочник." + ИмяСправочника);
    ```

Изменение имени таблицы. Вариант 2.

!!! success "Правильно"

    ```bsl hl_lines="7 11 13"
    ТекстЗапроса =
        "ВЫБРАТЬ
        |   Номенклатура.Наименование  КАК НаименованиеТовара,
        |   ЕСТЬNULL(ТаблицаОстатков.ВНаличииОстаток, 0) КАК ОстатокТовара
        |ИЗ
        |   Справочник.Номенклатура КАК Номенклатура
        |   ЛЕВОЕ СОЕДИНЕНИЕ #ТаблицаОстатков КАК ТаблицаОстатков
        |       ПО Номенклатура.Ссылка= ТаблицаОстатков.Номенклатура";

    Если ИспользуетсяАдресноеХранение Тогда
        ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТаблицаОстатков", "РегистрНакопления.ТоварыВЯчейках.Остатки");
    Иначе
        ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТаблицаОстатков", "РегистрНакопления.ТоварыНаСкладах.Остатки");
    КонецЕсли;
    ```

###### 7.2.

Использование комментария для помещения во временную таблицу результата запроса.

!!! failure "Неправильно"

    ```bsl hl_lines="4 9"
    ТекстЗапроса =
        "ВЫБРАТЬ
        |   Контрагенты.Ссылка КАК Ссылка
        |   // ПОМЕСТИТЬ втКонтрагенты
        |ИЗ
        |	  Справочник.Контрагенты КАК Контрагенты";

    Если ВыгрузитьВоВременнуюТаблицу Тогда
        ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "// ПОМЕСТИТЬ", "ПОМЕСТИТЬ");
    КонецЕсли;
    ```

!!! success "Правильно"

    ```bsl hl_lines="4 9"
    ТекстЗапроса =
        "ВЫБРАТЬ
        |   Контрагенты.Ссылка КАК Ссылка
        |ПОМЕСТИТЬ втКонтрагенты
        |ИЗ
        |   Справочник.Контрагенты КАК Контрагенты";

    Если Не ВыгрузитьВоВременнуюТаблицу Тогда
        ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ втКонтрагенты", "");
    КонецЕсли;
    ```

###### 7.3.

Конкатенация нескольких текстов запросов в пакет.

!!! failure "Неправильно"

    ```bsl  hl_lines="1 8 12"
    ТекстЗапроса = " ";
    Если ИспользоватьУпаковки Тогда
        ТекстЗапроса =
            "ВЫБРАТЬ
            |   Упаковки.Ссылка КАК Ссылка
            |ИЗ
            |   Справочник.Упаковки КАК Упаковки;
            |/////////////////////////////////////////////////////////////
            |";
    КонецЕсли;

    ТекстЗапроса = ТекстЗапроса +
        "ВЫБРАТЬ
        |   Номенклатура.Ссылка КАК Ссылка
        |ИЗ
        |   Справочник. Номенклатура КАК Номенклатура";
    ```

!!! success "Правильно"

    ```bsl hl_lines="1 9-13 16"
    ТекстЗапроса = " ";
    Если ИспользоватьУпаковки Тогда
        ТекстЗапроса =
            "ВЫБРАТЬ
            |  Упаковки.Ссылка КАК Ссылка
            |ИЗ
            |   Справочник.Упаковки КАК Упаковки";

        ТекстЗапроса = ТекстЗапроса +
            "
            |;
            |/////////////////////////////////////////////////////////////
            |";
    КонецЕсли;

    ТекстЗапроса = ТекстЗапроса +
      "ВЫБРАТЬ
      |   Номенклатура.Ссылка КАК Ссылка
      |ИЗ
      |   Справочник.Номенклатура КАК Номенклатура";
    ```

Или

!!! success "Правильно"

    ```bsl hl_lines="1-5 7 15 23 24"
    Разделитель =
        "
        |;
        |/////////////////////////////////////////////////////////////
        |";

    ТекстыЗапросовПакета = Новый Массив;

    ТекстЗапроса =
        "ВЫБРАТЬ
        |   Упаковки.Ссылка КАК Ссылка
        |ИЗ
        |   Справочник.Упаковки КАК Упаковки";

    ТекстыЗапросовПакета.Добавить(ТекстЗапроса);

    ТекстЗапроса =
        "ВЫБРАТЬ
        |   Номенклатура.Ссылка КАК Ссылка
        |ИЗ
        |   Справочник.Номенклатура КАК Номенклатура";

    ТекстыЗапросовПакета.Добавить(ТекстЗапроса);
    ТекстЗапроса = СтрСоединить(ТекстыЗапросовПакета, Разделитель);
    ```

###### См. также

- [Запросы, динамические списки и отчеты на СКД: требования по локализации](762.md)

###### Источник

https://its.1c.ru/db/v8std#content:437
