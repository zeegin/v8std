###### #std640

# Параметры процедур и функций

###### 1.
Параметры методов надо именовать так же, как переменные.

См. [#std454: Правила образования имен переменных](454.md)

###### 2.
Используйте параметры методов.
Избегайте неявную передачу, такую как переменные модулей или реквизиты формы.

###### 3.
Используйте порядок параметров: от общего к частному.

!!! failure "Неправильно"

    ```bsl 
    Процедура ПересчитатьСуммуДокумента(ИмяПоляСумма, ДокументОбъект, СуммаВключаетНДС = Истина)
 
    Процедура ПоменятьЦветПоляФормы(Цвет, ИмяПоля, Форма)
    ```

!!! success "Правильно"

    `#!bsl ДокументОбъект` и `#!bsl Форма` разместить впереди:

    ```bsl
    Процедура ПересчитатьСуммуДокумента(ДокументОбъект, ИмяПоляСумма, СуммаВключаетНДС = Истина)
 
    Процедура ПоменятьЦветПоляФормы(Форма, ИмяПоля, Цвет)
    ```

###### 4.
Необязательные параметры (параметров по-умолчанию) располагайте после обязательных.

!!! success "Правильно"

    ```bsl
    Функция КурсВалютыНаДату(Валюта, Дата = Неопределено) Экспорт
    ```

###### 5.

Не делайте больше 7 параметров.
Не делайте больше 3 необязательных параметров (параметров по-умолчанию).

Вместо большого количества параметров:

- группируйте похожие параметры в один типа `#!bsl Структура`, такие как `#!bsl ДанныеЗаполнения`, `#!bsl ПараметрыПроведения`, `#!bsl ДанныеФайла`
- пересмотрите логику работы метода, разбейте логику на несколько методов

!!! failure "Неправильно"

    ```bsl 
    // Добавляет новое поле на форму, инициализирует его значениями по умолчанию.
    Функция ДобавитьПолеФормы(ИмяПоля, Заголовок = Неопределено, ОбработчикПриИзменении = "", ОбработчикНачалоВыбора = "",
        ШиринаПоля, ЦветФона = Неопределено, ЦветФонаЗаголовка = Неопределено, Родитель = Неопределено,
        КартинкаШапки = Неопределено, ПутьКДанным = Неопределено, ТолькоПросмотрПоля = Ложь,
        СвязиПараметровВыбора = Неопределено)
        …
    КонецФункции

    // вызывающий код
    НовоеПоле = ДобавитьПолеФормы("СтараяЦена", НСтр("ru='Цена'"),,, 12, ЦветФона, ЦветЗаголовка, НоваяГруппа,,,Истина);
    НовоеПоле.ЦветТекста = WebЦвета.Серый;
    ```

    ```bsl 
    // Создает элемент справочника "Номенклатура"
    Процедура СоздатьЭлементНоменклатуры(Наименование, ТоварУслуга, ЕдиницаИзмерения, ВесНетто, ПроверятьУникальность = Истина)
        …
    КонецПроцедуры
    ```

!!! success "Правильно"

    `#!bsl ИмяПоля` оставить как ключевой параметр, логику пересмотреть:

    ```bsl
    // Добавляет новое поле на форму, инициализирует его значениями по умолчанию.
    Функция НовоеПолеФормы(ИмяПоля)  
        …
    КонецФункции

    // вызывающий код
    НовоеПоле = НовоеПолеФормы("СтараяЦена");
    НовоеПоле.Заголовок  = НСтр("ru='Цена'");
    НовоеПоле.ЦветФона   = ЦветФона;
    НовоеПоле.ЦветТекста = WebЦвета.Серый;
    НовоеПоле…. = …
    ```

###### 6.
Избегайте громоздких конструкций.

###### 6.1.
Избегайте вложенные вызовы. Передавайте параметры явно.

!!! failure "Неправильно"

    ```bsl 
    СтруктураВложений.Вставить(ПрисоединенныйФайл.Наименование,
        Новый Картинка(ПолучитьИзВременногоХранилища(
            ПрисоединенныеФайлы.ПолучитьДанныеФайла(ПрисоединенныйФайл.Ссылка).СсылкаНаДвоичныеДанныеФайла)));
    ```

!!! success "Правильно"

    Разбивайте параметры с помощью логических переменных:

    ```bsl
    АдресФайлаИзображения = ПрисоединенныеФайлы.ПолучитьДанныеФайла(ПрисоединенныйФайл.Ссылка).СсылкаНаДвоичныеДанныеФайла;
    ДанныеИзображения = Новый Картинка(ПолучитьИзВременногоХранилища(АдресФайлаИзображения));
    СтруктураВложений.Вставить(ПрисоединенныйФайл.Наименование, ДанныеИзображения);
    ```

    Чувствуете легкость и компактность? Оставьте как есть!
    ```bsl
    Предупреждение(НСтр("ru='Для выполнения операции необходимо установить расширение работы с файлами.'"));

    ПеречитатьСуммуПоКурсу(Сумма, КурсВалютыНаДату(Валюта));
    ```

###### 6.2.
Избегайте вложенные конструкторы.
Вложенные структуры создавайте только если свойств структуры 3 и меньше.

!!! failure "Неправильно"

    ```bsl 
    ЗаполнитьЦены(Объект.Товары, ,
        Новый Структура("Дата, Валюта, Соглашение, ПоляЗаполнения", Объект.Дата, Объект.Валюта, Объект.Соглашение, "Цена, СтавкаНДС, ВидЦены, СрокПоставки"),
            Новый Структура("ПересчитатьСумму, ПересчитатьСуммуСНДС, ПересчитатьСуммуНДС, ПересчитатьСуммуРучнойСкидки, ОчиститьАвтоматическуюСкидку,
            ОчиститьСуммуВзаиморасчетов", "КоличествоУпаковок", СтруктураПересчетаСуммы, СтруктураПересчетаСуммы,
            "КоличествоУпаковок", Неопределено, Неопределено));
    ```

!!! success "Правильно"

    ```bsl 
    ПараметрыЗаполнения = Новый Структура;
    ПараметрыЗаполнения.Вставить("Дата", Объект.Дата);
    ПараметрыЗаполнения.Вставить("Валюта", Объект.Валюта);
    ПараметрыЗаполнения.Вставить("Соглашение", Объект.Соглашение);
    ПараметрыЗаполнения.Вставить("ПоляЗаполнения", "Цена, СтавкаНДС, ВидЦены, СрокПоставки");
    
    ДействияСИзмененнымиСтроками = Новый Структура;
    ДействияСИзмененнымиСтроками.Вставить("ПересчитатьСумму","КоличествоУпаковок");
    ДействияСИзмененнымиСтроками.Вставить("ПересчитатьСуммуСНДС", ПараметрыПересчетыСуммы);
    ДействияСИзмененнымиСтроками.Вставить("ПересчитатьСуммуНДС", ПараметрыПересчетыСуммы);
    ДействияСИзмененнымиСтроками.Вставить("ПересчитатьСуммуРучнойСкидки","КоличествоУпаковок");
    ДействияСИзмененнымиСтроками.Вставить("ОчиститьАвтоматическуюСкидку");
    ДействияСИзмененнымиСтроками.Вставить("ОчиститьСуммуВзаиморасчетов ");

    ЗаполнитьЦены(Объект.Товары, ПараметрыЗаполнения, ДействияСИзмененнымиСтроками);
    ```

###### 6.3.
Избегайте лишних пробелов:

- перед открывающей скобкой
- после открывающей скобки
- перед закрывающей скобкой
- перед запятыми

!!! failure "Неправильно"

    ```bsl 
    НовоеПоле = НовоеПолеФормы ( ИмяПоля , ТипПоля );
    ```

!!! success "Правильно"

    ```bsl 
    НовоеПоле = НовоеПолеФормы(ИмяПоля, ТипПоля);
    ```

!!! quote "В стандарте не указано"

    Полную формулировку __"перед запятыми при размещении параметров в одной строке и если параметры не опущены"__ можно трактовать как разрешения писать слитно запятые у необязательных параметров

    ```bsl 
    Форма = ПолучитьФорму(ИмяФормы, , , , , , Параметр);
    Форма = ПолучитьФорму(ИмяФормы,,,,,, Параметр);
    ```

    равносильно допустимы

###### 7.
При вызове методов не пропускайте обязательные параметры.
Пропущенный обязательный параметр получит значение `#!bsl Неопределено`, а метод может не уметь с ним работать.

Для вызова методы:
```bsl 
Процедура ПоменятьЦветПоляФормы(Форма, ИмяПоля, Цвет)
```

!!! failure "Неправильно"

    ```bsl 
    ПоменятьЦветПоляФормы(,"РезультатПроверки", ЦветаСтиля.ПоясняющийОшибкуТекст); // пропущен первый параметр Форма
    ПоменятьЦветПоляФормы(,,); // пропущены все обязательные параметры 
    ```

!!! success "Правильно"

    ```bsl 
    ПоменятьЦветПоляФормы(ЭтотОбъект, "РезультатПроверки", Цвет); // указаны все обязательные параметры
    ```

###### См. также

- :fontawesome-solid-book-bookmark: СОВЕРШЕННЫЙ КОД Стив Макконнелл ГЛАВА 7. Высококачественные методы
- :fontawesome-solid-book-bookmark: ЧИСТЫЙ КОД Роберт Мартин ГЛАВА 3. Функции
- :fontawesome-solid-book-bookmark: Чистый дизайн Кент Бек ГЛАВА 8. Пояснительные переменные
- :fontawesome-solid-book-bookmark: Чистый дизайн Кент Бек ГЛАВА 10. Явная передача параметров
- [Передача параметров по ссылке и по значению при вызове процедур и функций](https://its.1c.ru/db/metod8dev/content/2606/hdoc)
- [Руководство разработчика 4.7.4. Передача параметров процедур и функций](https://its.1c.ru/db/v8327doc#bookmark:dev:ti000000151)
- [#std641: Структуры и таблицы значений в качестве параметров процедур и функций](641.md)

###### Источник

https://its.1c.ru/db/v8std#content:640
